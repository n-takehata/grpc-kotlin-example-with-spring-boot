buildscript {
	ext.kotlin_version = "1.3.72"
	ext.coroutines_version = "1.3.3"
	ext.protobuf_version = "3.11.1"
	ext.grpc_version = "1.28.1" // CURRENT_GRPC_VERSION
	ext.grpc_kotlin_version = "0.1.1" // CURRENT_GRPC_KOTLIN_VERSION

	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id("org.springframework.boot") version "2.2.6.RELEASE"
	id("org.jetbrains.kotlin.jvm") version "1.3.61"
	id("org.jetbrains.kotlin.plugin.spring") version "1.3.61"
	id("io.spring.dependency-management") version "1.0.7.RELEASE"
	id("com.google.protobuf") version "0.8.9"
	id( "idea")
	id( "java")
}

group = "com.example.grpc.kotlin"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
	mavenCentral()
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-webflux") // ①spring-boot-starter-webfluxを使う
	implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
	implementation("org.jetbrains.kotlin:kotlin-reflect")
	implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
	implementation("io.grpc:grpc-kotlin-stub:$grpc_kotlin_version")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$coroutines_version")
	implementation("io.github.lognet:grpc-spring-boot-starter:3.5.3")
	testImplementation("org.springframework.boot:spring-boot-starter-test")
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:$protobuf_version"
	}

	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
		}
		grpckt {
			artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version" // ②
		}
	}

	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc { }
				grpckt { } // ③
			}
		}
	}
}
